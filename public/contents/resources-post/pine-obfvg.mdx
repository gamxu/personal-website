---
title: "OB+FVG Pine Script indicator"
description: "Draw Order Block and FVG zone use in forex pair trading"
imgPath: "pine-obfvg.png"
startDate: "Jan 2025"
category: ""
---

### TradingView Pine Script Code 
This is a TradingView Pine Script codes which are not published in TradingView.

### How to add the Pine Script Code to TradingView Chart?
1. Open TradingView chart.
2. Click "Pine Editor" at the bottom.
3. Copy indicator code and Paste it in Pine Editor.
4. Click "Save" in Pine Editor.
5. Click "Add to Chart" in Pine Editor.
6. Your indicator will be displayed on the chart.

### Purpose of this indicator
This indicator is use along with my personal trading setup which include Order Block, Fair Value Gap, and Liquidity. In order for you to benefit the best out if this indicator, you will require the basic knowledge and experience of the 3 techniques previously listed.

### Disclaimer
The information and publications are not intended to be, and do not constitute, financial, investment, trading, or any other form of advice or recommendations supplied or endorsed by the publisher or the creators of this script. Users are advised to exercise their own judgment and seek professional advice before making any financial decisions.

### FeedBack
If you find this indicator improves your trading or helps you make money, consider giving me a review in "Give FeedBack" Tab!.

```bash
// This Pine Script™ code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © Did4677

//@version=6
indicator("Order Block+ FVG Detection", overlay=true)

// Inputs
inputImbalanceMultiplierMin = input.float(2.0, title="Imbalance Multiplier Min")
inputShowZones = input.bool(true, title="Show Order Block Zones")
inputShowFVGs = input.bool(true, title="Show Fair Value Gaps")
inputMaxZones = input.int(10, title="Amount of Order Block Zones can be shown")
inputMaxFVGs = input.int(10, title="Amount of Fair Value Gaps can be shown")

// Structure to store order block information
type OrderBlock
    box     id
    float   high
    float   low
    bool    isBull
    int     startIndex
    bool    isActive

// Structure to store FVG information
type FVG
    box     id
    float   high
    float   low
    bool    isBull
    int     startIndex
    bool    isActive

var orderBlocks = array.new<OrderBlock>()
var fvgs = array.new<FVG>()

isCurrentBull = close > open
isCurrentBear = close < open

totalHLlength = high[1] - low[1]
topWick = math.abs(high[1] - math.max(open[1], close[1])) 
lowwick = math.abs(math.min(open[1], close[1]) - low[1])
totalWickLength = topWick + lowwick

// imbalance need to be a strong candle and is 2-3 time bigger than the balance base candle
isImbalanceCandle(leftBody, rightBody, currentBody) => 
    rightBody + currentBody >= leftBody * inputImbalanceMultiplierMin and totalWickLength <= 0.3 * totalHLlength

// Check if price retests an order block
isRetested(ob) =>
    if ob.isBull
        low <= ob.high and high >= ob.high
    else
        high >= ob.low and low <= ob.low

// Check if price retests a FVG
isRetestedFVG(fvg) =>
    // Calculate the midpoint of the gap
    gapMidpoint = if fvg.isBull
        fvg.low + (fvg.high - fvg.low) * 0.5  // For bullish FVG
    else
        fvg.high - (fvg.high - fvg.low) * 0.5  // For bearish FVG
    
    // Check if price has reached 50% of the gap
    if fvg.isBull
        low <= gapMidpoint  // Price needs to reach halfway into bullish gap
    else
        high >= gapMidpoint  // Price needs to reach halfway into bearish gap

// Calculating candle sizes
leftBody = math.abs(high[2] - low[2])
rightBody = math.abs(close[1] - open[1])
currentBody = math.abs(close - open)

// Identify imbalance and order block
isBullImbalance = isImbalanceCandle(leftBody, rightBody, currentBody) and (close[1] > open[1])
isBearImbalance = isImbalanceCandle(leftBody, rightBody, currentBody) and (close[1] < open[1])

// Handle Fair Value Gaps
if inputShowFVGs
    if (low > high[2]) and isBullImbalance and isCurrentBull
        box fvgBox = box.new(bar_index[2], high[2], bar_index + 100, low, 
             border_color=color.new(color.gray, 100), 
             bgcolor=color.new(color.gray, 90))
        fvg = FVG.new(fvgBox, high[2], low, true, bar_index[2], true)
        array.push(fvgs, fvg)
    else if (high < low[2]) and isBearImbalance and isCurrentBear
        box fvgBox = box.new(bar_index[2], low[2], bar_index + 100, high, 
             border_color=color.new(color.gray, 100), 
             bgcolor=color.new(color.gray, 90))
        fvg = FVG.new(fvgBox, low[2], high, false, bar_index[2], true)
        array.push(fvgs, fvg)

// Create new order blocks
if inputShowZones
    if isBullImbalance and isCurrentBull
        colorBox = color.new(color.blue, 80)
        box zone = box.new(bar_index[2], high[2], bar_index + 100, low[2], 
             border_color=color.new(color.blue, 0), 
             bgcolor=colorBox)
        ob = OrderBlock.new(zone, high[2], low[2], true, bar_index[2], true)
        array.push(orderBlocks, ob)
    else if isBearImbalance and isCurrentBear
        colorBox = color.new(color.red, 80)
        box zone = box.new(bar_index[2], high[2], bar_index + 100, low[2], 
             border_color=color.new(color.red, 0), 
             bgcolor=colorBox)
        ob = OrderBlock.new(zone, high[2], low[2], false, bar_index[2], true)
        array.push(orderBlocks, ob)

// Check for retests and update order blocks
if array.size(orderBlocks) > 0
    for i = array.size(orderBlocks) - 1 to 0
        ob = array.get(orderBlocks, i)
        if ob.isActive and isRetested(ob)
            box.set_right(ob.id, bar_index)
            ob.isActive := false
            array.set(orderBlocks, i, ob)

// Check for retests and update FVGs
if array.size(fvgs) > 0
    for i = array.size(fvgs) - 1 to 0
        fvg = array.get(fvgs, i)
        // Only check for retest if we're past candles that create it
        if fvg.isActive and (bar_index > fvg.startIndex + 3)
            if isRetestedFVG(fvg)
                box.set_right(fvg.id, bar_index)
                fvg.isActive := false
                array.set(fvgs, i, fvg)

// Clean up old zones
if bar_index > 500
    while array.size(orderBlocks) > inputMaxZones
        firstOb = array.shift(orderBlocks)
        box.delete(firstOb.id)
    while array.size(fvgs) > inputMaxFVGs
        firstFvg = array.shift(fvgs)
        box.delete(firstFvg.id)
```