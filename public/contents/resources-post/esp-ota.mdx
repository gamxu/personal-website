---
title: "ESP32 Sensor logging with Over The Air control"
description: "IoT prototype use for DIPROM project"
imgPath: "esp-ota.png"
startDate: "Dec 2023"
category: ""
---
the IoT prototype device using ESP32 that receives data from sensors and creates logs on the Firebase Realtime database, uses for present, and helps win the project auction from DIPROM
- Using ESP32 as main controller
- Use Over The Air(OTA) library to update sketches wirelessly
- Use Firebase_ESP_Client library to connect to Firebase Realtime Database(RTDB)
> I use Temp sensor in this case, but this will works well with any sensors or any use cases.

```
#include <Arduino.h>
#include <ArduinoOTA.h>
#include <WiFi.h>
#include <Firebase_ESP_Client.h>
#include "DHT.h"
#include "time.h"

// Provide the token generation process info.
#include "addons/TokenHelper.h"
// Provide the RTDB payload printing info and other helper functions.
#include "addons/RTDBHelper.h"

// Insert your network credentials
#define WIFI_SSID ""
#define WIFI_PASSWORD ""
// Insert Firebase project API Key
#define API_KEY ""

// Insert Authorized Email and Corresponding Password
#define USER_EMAIL ""
#define USER_PASSWORD ""

// Insert RTDB URLefine the RTDB URL
#define DATABASE_URL ""

#define DHTPIN 14

// Define Firebase objects
FirebaseData fbdo;
FirebaseAuth auth;
FirebaseConfig config;

// Variable to save USER UID
String uid;

// Database main path (to be updated in setup with the user UID)
String databasePath;
// Database child nodes
String tempPath = "/temperature";
String timePath = "/timestamp";

// Parent Node (to be updated in every loop)
String parentPath;

int timestamp;
FirebaseJson json;

//To get time from time server
const char* ntpServer = "pool.ntp.org";

// Create DHT obj output: pin14 type:DHT11
DHT dht(DHTPIN, DHT11);
float temperature;

// Timer variables (send new readings every 1 minutes)
unsigned long sendDataPrevMillis = 0;
unsigned long timerDelay = 60000;

// Initialize WiFi
void initWiFi() {
  WiFi.begin(WIFI_SSID, WIFI_PASSWORD);
  Serial.print("Connecting to WiFi ..");
  while (WiFi.status() != WL_CONNECTED) {
    Serial.print('.');
    delay(1000);
  }
  Serial.println(WiFi.localIP());
  Serial.println();
}

// Function that gets current epoch time
unsigned long getTime() {
  time_t now;
  struct tm timeinfo;
  if (!getLocalTime(&timeinfo)) {
    //Serial.println("Failed to obtain time");
    return(0);
  }
  time(&now);
  return now;
}

int portAna=0;
void setup() {
  Serial.begin(9600);
  dht.begin();
  initWiFi();
  configTime(0, 0, ntpServer);

  //Start OTA
  ArduinoOTA.begin(); 

  // Assign the api key (required)
  config.api_key = API_KEY;

  // Assign the user sign in credentials
  auth.user.email = USER_EMAIL;
  auth.user.password = USER_PASSWORD;

  // Assign the RTDB URL (required)
  config.database_url = DATABASE_URL;

  Firebase.reconnectWiFi(true);
  fbdo.setResponseSize(4096);

  // Assign the callback function for the long running token generation task */
  config.token_status_callback = tokenStatusCallback; //see addons/TokenHelper.h

  // Assign the maximum retry of token generation
  config.max_token_generation_retry = 5;

  // Initialize the library with the Firebase authen and config
  Firebase.begin(&config, &auth);

  // Getting the user UID might take a few seconds
  Serial.println("Getting User UID");
  while ((auth.token.uid) == "") {
    Serial.print('.');
    delay(1000);
  }

  // Print user UID
  uid = auth.token.uid.c_str();
  Serial.print("User UID: ");
  Serial.println(uid);

  // Update database path
  databasePath = "/UsersData/" + uid + "/readings"; 

}

void loop() {
  //Handle OTA
  ArduinoOTA.handle(); 

  // Send new readings to database
  if (Firebase.ready() && (millis() - sendDataPrevMillis > timerDelay || sendDataPrevMillis == 0)){
  sendDataPrevMillis = millis();
  
  //Get current timestamp
  timestamp = getTime();
  Serial.print ("time: ");
  Serial.println (timestamp);

  parentPath= databasePath + "/" + String(timestamp); //parentPath = UsersData/<users_uid*>/timestamp

  String tempStr = String(dht.readTemperature());
  if(tempStr != "nan"){
    //add data to json obj
    json.set(tempPath.c_str(), tempStr);
    json.set(timePath, String(timestamp));
    //append data to RTDB
    Serial.printf("Set json... %s\n", Firebase.RTDB.setJSON(&fbdo, parentPath.c_str(), &json) ? "ok" : fbdo.errorReason().c_str());    
  }else{
    Serial.println ("Throw: data type nan");
  }

  }
}
```
